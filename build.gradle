plugins {
  id 'eclipse'
  id 'com.diffplug.eclipse.apt' version '3.43.0' apply false
  id 'io.codearte.nexus-staging' version '0.30.0' apply false 
}

def calculateReleaseUrl(version) {
  if (rootProject.hasProperty('repositoryId')) {
    return 'https://s01.oss.sonatype.org/service/local/staging/deployByRepositoryId/' + rootProject.repositoryId
  }
  else if (version.endsWith('SNAPSHOT')) {
    return 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
  }
  else {
    return 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
  }
}

allprojects { project ->

  group = 'io.netlibs.ami'
  version = '0.11.0-SNAPSHOT'

  repositories {
    mavenCentral()
  }

  project.plugins.withId('java') {
    java {
      toolchain {
        languageVersion = JavaLanguageVersion.of(21)
      }
    }
    project.apply from: "${rootDir}/gradle/ossrh.gradle"
    

    tasks.withType(JavaCompile) {
      options.encoding = 'UTF-8'
      options.compilerArgs += "--enable-preview" // for structured concurrency in 21.
    }
    
    test {
      jvmArgs(['--enable-preview'])
      useJUnitPlatform()
    }
    
  }
  
  project.apply plugin: 'eclipse'
  project.apply from: "${rootDir}/gradle/eclipse.gradle"

}

task configureBuildship {
}

eclipse.project.name = 'netlibs:ami'
eclipse.synchronizationTasks configureBuildship

wrapper {
  gradleVersion = '8.4'
  doLast {
    delete "${projectDir}/gradlew.bat"
  }
}



/*
 * Sonatype Staging Finalization
 * ====================================================
 *
 * When publishing to Maven Central, we need to close the staging
 * repository and release the artifacts after they have been
 * validated. This configuration is for the root project because
 * it operates at the "group" level.
 */
 
if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
  apply plugin: "io.codearte.nexus-staging"
  nexusStaging {
    serverUrl = "https://s01.oss.sonatype.org/service/local/"
    packageGroup = "io.netlibs"
    // stagingRepositoryId
    stagingProfileId = "395cd0d126dacd"
    username = project.property("ossrhUsername")
    password = project.property("ossrhPassword")
  }
}
